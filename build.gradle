buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'com.adaptc.gradle:nexus-workflow:0.6'
  }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.2.3'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'findbugs'

apply plugin: 'nexus-workflow'

scmVersion {
    tag {
        prefix = 'regexp-trie'
    }
}

project.version = scmVersion.version

group = 'me.geso'
archivesBaseName = "regexp-trie"

description = """regexp-trie"""

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
sourceCompatibility = 1.7
targetCompatibility = 1.7

nexusStaging {
  packageGroup = "me.geso"
}

repositories {
     maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version:'4.11'
}

findbugsMain {
  ignoreFailures = false
  effort = "max"
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

try {
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment {
          MavenDeployment deployment -> signing.signPom(deployment)
        }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name 'regexp-trie'
          packaging 'jar'
          // optionally artifactId can be defined here 
          description 'Trie based regexp generator'
          url 'http://github.com/tokuhirom/regexp-trie/'

          scm {
            url 'https://github.com/tokuhirom/regexp-trie/'
            connection 'scm:git:git://github.com/tokuhirom/regexp-trie.git'
            developerConnection 'scm:git:git@github.com:tokuhirom/regexp-trie.git'
          }

          licenses {
            license {
              name 'MIT License'
              url 'http://www.opensource.org/licenses/mit-license.php'
            }
          }

          developers {
            developer {
              id 'tokuhirom'
              name 'Tokuhiro Matsuno'
              email 'tokuhirom@gmail.com'
            }
          }
        }
      }
    }
  }
} catch (MissingPropertyException mpe) {
  if (System.env['CI']) {
    println("Run on CI");
  } else {
    throw mpe;
  }
}
